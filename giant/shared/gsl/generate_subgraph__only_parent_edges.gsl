// GSL-expression to generate a subgraph containing only nodes
//   connected with edges belonging to "*.Parent"
//
// First Author: Oliver Kopp
// based on iterate_find_system.gsl
//
// $Id: generate_subgraph__only_parent_edges.gsl,v 1.1 2003/10/06 18:26:23 koppor Exp $

[
  set (+target_edges, empty_edge_set ());
  set (+target_nodes, empty_node_set ());

  // build new activation environment. This makes
  // it possible to have 'nodes' destroyed before
  // the subgraph is created --> wastes less memory.
  { ()
    [
      set (+edges, all_edges ());
      loop
       ({ ()
          [
            set (+e, get_first ('edges));
            sub ('edges, e);
            if
             (instance_of (e, "parent_edge_class_set"),
              { () 
	        [
		add ('target_edges, e);
		add ('target_nodes, get_source (e));
		add ('target_nodes, get_target (e));
		]
              },
              null);

	    // end loop, until edges is empty
            less (0, size_of ('edges));
          ]
        });
    ]
  } ();

  set ('subgraph.Tree_Parent_Edges, (target_nodes, target_edges));
]
